// Construct a domain Omega
// with an inclusion omega

// Omega = B((0,0),1), omega is a slice of Omega with angle pi/6
real R = 1;

int N = 30;  // Number of points on boundary

//omega
border C1(t=0,pi/6){
   x = R*cos(t);
   y = R*sin(t);
   label = 3;
}
border L1(t=0,1){
   x = t*R;
   y = 0;
   label = 1;
}
border L2(t=0,1){
   x = cos(pi/6)*t*R;
   y = sin(pi/6)*t*R;
   label = 2;
}

//Omega
border C2(t=pi/6,2*pi){
   x = R*cos(t);
   y = R*sin(t);
   label = 0;
}

// build the mesh
mesh Th = buildmesh(C2(2*pi*R*N)+C1(pi/6*R*N)+L1(N)+L2(N));

plot(Th,wait=1);

// get region numbers
int Omega1 = Th(-0.5*R,-0.5*R).region;
int Omega2 = Th(0.5*R,0.1*R).region;

// Finite element spaces
fespace Vh(Th,P1);

// define the constant u in the problem

// finite element function for the temperature and
// test function
Vh T,phi;
Vh T0 = 10; // finite element function for target temperature

Vh u1,u2;
u1 = -y;
u2 = x;
real fact = 1; /// change this factor to diminish or increase
                // convective effects

plot([u1,u2],wait=1,cmm="Convection vector field");

real v = 1;

macro grad(u) [dx(u),dy(u)] //  macro for gradient

// FV for heat equation with convection
// (think why for the given vector field, the convection does not
// seem to play any role when the two disks are concentric?
//
// Change the center of the small disk and the magnitude of the
// convection factor to observe the differences
problem Temp(T,phi) =
   int2d(Th)(grad(T)'*grad(phi))
  +int2d(Th)(fact*[u1,u2]'*grad(T)*phi)
  -int2d(Th,Omega1)(v*phi)
  +on(2,T=0);

Temp;

plot(T,fill=1,cmm="Heat Equation with Convection",value=1);
