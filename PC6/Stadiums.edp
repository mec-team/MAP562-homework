// MAP 562
// G. Allaire, B. Bogosel

// Geometric optimization of a parametric hole

real a=0.5,b=0.3,l=2.5;
real cx = 0,cy=0;
int maxiter = 2;
real maxstep = 0.01;
real ratio = 1;

real fixedV = 1;

real step = 0.01;

verbosity = 0;

real alpha =0.5;

real E=1;			// Young modulus
real nu=0.3;			// Poisson coefficient (between -1 and 1/2)

real lambda=E*nu/((1.+nu)*(1.-2.*nu));
real mu=E/(2.*(1.+nu));

int NN = 10;
int Ni = 10;

int GammaD = 0,GammaN = 1,Gamma0=20;

border left(t=l,-l){x=-l;y=t;label=GammaD;}
border bot(t=-l,l){x=t;y=-l;label=GammaN;}
border right(t=-l,l){x=l;y=t;label=GammaN;}
border top(t=l,-l){x=t;y=l;label=GammaN;}

// Parametric boundary
border wleft(t=pi/2,3*pi/2){x=cx-a/2+b/2*cos(t);y=cy+b/2*sin(t);label=Gamma0;}
border wbot(t=-a/2,a/2){x=cx+t;y=cy-b/2;label=Gamma0;}
border wright(t=-pi/2,pi/2){x=cx+a/2+b/2*cos(t);y=cy+b/2*sin(t);label=Gamma0;}
border wtop(t=a/2,-a/2){x=cx+t;y=cy+b/2;label=Gamma0;}

plot(left(2*NN)+bot(2*NN)+right(2*NN)+top(2*NN)
                   +wleft(b/2*pi*NN)+wbot(a*NN)+wright(b/2*pi*NN)+wtop(a*NN));

macro projparams(a,b)
      ratio = sqrt((0.25*b^2*pi+a*b)/fixedV);
      a = a/ratio;
      b = b/ratio;//

projparams(a,b);

mesh Th = buildmesh(left(2*NN)+bot(2*NN)+right(2*NN)+top(2*NN)
                   +wleft(b/2*pi*NN)+wbot(a*NN)+wright(b/2*pi*NN)+wtop(a*NN));

plot(Th);

fespace Vh(Th,P1);
fespace Vh0(Th,P0);

Vh ux,uy,vx,vy;


real sqrt2 = sqrt(2);
macro e(ux,uy) [dx(ux),dy(uy),(dx(uy)+dy(ux))/sqrt2]//
macro div(ux,uy) (dx(ux)+dy(uy))//
macro Aeueu(ux,uy) (2*mu*e(ux,uy)'*e(ux,uy)+lambda*div(ux,uy)*div(ux,uy))//

problem elasticity([ux,uy],[vx,vy],solver=sparsesolver) =
  int2d(Th)(2*mu*e(ux,uy)'*e(vx,vy)+lambda*div(ux,uy)*div(vx,vy))
 -int1d(Th,GammaN)(N.x*vx+alpha*N.y*vy)
 +on(GammaD,ux=0,uy=0);

elasticity;
plot([ux,uy]);

real compliance = int2d(Th)(Aeueu(ux,uy));
cout << "Compliance: " << compliance << endl;

real compliance2 = int1d(Th,GammaN)(N.x*ux+alpha*N.y*uy);
cout << "Compliance: " << compliance2 << endl;

Vh0 grad;
grad = -Aeueu(ux,uy);

real valold = 1e100;

real aold,bold,cxold,cyold;


aold = a;
bold = b;
cxold = cx;
cyold = cy;

for(int i=1;i<maxiter;i++){
   // Compute angles in polar coordinates
   Vh thetas = atan2(y,x);
   plot(thetas,fill=1,wait=1,cmm="Compute the angles for the shape derivative");

   // Compute here the derivatives with respect to a and b
   // use the variable thetas defined above to have the angle in polar coordinates
   real deriva = 0;
   real derivb = 0;
   // Compute here the derivatives with respect to cx and cy
   real derivx = int1d(Th,Gamma0)(grad*N.x);
   real derivy = int1d(Th,Gamma0)(grad*N.y);

   real nxy = sqrt(derivx^2+derivy^2);
   real nab = sqrt(deriva^2+derivb^2);

   // normalize gradients since the difference is too big sometimes...
   // note that this also gives a descent direction
   derivx = derivx/nxy;
   derivy = derivy/nxy;
   deriva = deriva/nab;
   derivb = derivb/nab;

   // Gradient descent step
   cx = cx-step*derivx;
   cy = cy-step*derivy;
   a  = a-step*deriva;
   b  = b-step*derivb;

   // check to see if the stadium can fit into the square
   b  = max(0.01,min(2*l-0.01,b));
   a  = max(0.01,min(2*l-0.01-b,a));



   // project on the area constraint
   projparams(a,b);

   // make sure that the center of the ellipse is compatible with the ellipse being interior to the square
   cx = max(cx,-l+0.5*(a+b)+0.1);
   cx = min(cx,l-0.5*(a+b)-0.1);
   cy = max(cy,-l+0.5*b+0.1);
   cy = min(cy,l-0.5*b-0.1);



   Th = buildmesh(left(2*NN)+bot(2*NN)+right(2*NN)+top(2*NN)
                   +wleft(b/2*pi*NN)+wbot(a*NN)+wright(b/2*pi*NN)+wtop(a*NN));


   plot(Th,wait=1);

   elasticity;
   real compliance = int2d(Th)(Aeueu(ux,uy));


   if(compliance<valold){
       cout << "Accept" << endl;
       cout << "Compliance: " << compliance << endl;
        grad = -Aeueu(ux,uy);
        step = min(maxstep,1.1*step);
        aold = a;
        bold = b;
        cxold = cx;
        cyold = cy;
        valold = compliance;
   }
   else{
        cout << " Reject" << endl;
        step = step*0.5;
        a = aold;b=bold;cx=cxold;cy=cyold;
   }



}
